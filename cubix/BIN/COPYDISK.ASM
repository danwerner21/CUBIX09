*
* SYSTEM MEMORY LOCATIONS FOR USE BY APPLICATION PROGRAMS
*
OSRAM	EQU	$2000		USER APPLICATION RAM STARTS HEDE
OSEND	EQU	$DBFF		LAST LOCATION OF USER (NON-SYSTEM) RAM
OSUTIL	EQU	$D000		RAM FOR UTILITY PROGRAM USAGE
*
*
* COPYDISK: Disk to Disk direct copy utility
*
* Copyright 1983-2005 Dave Dunfield
* All rights reserved.
*
MAXBUF	EQU	OSEND-BUFFER/512 # SECTORS TO BUFFER
*
	ORG	OSRAM		PROGRAM LOCATION
* TEST FOR HELP REQUEST
CPYDSK	CMPA	#'?'		HELP?
	BNE	QUAL		NO
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	25		SYSTEM REQUEST NUMBER
	FCCZ	'Use: COPYDISK[/PROMPT/QUIET] <source drive> <dest drive>'
	RTS
* TEST FOR QUALIFIERS
QUAL	LDA	,Y		GET CHAR FROM LINE
	CMPA	#'/'		QUALIFIER?
	BNE	MAIN		NO, START UP PGM
	LDX	#QTABLE		PT TO TABLE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	18		SYSTEM REQUEST NUMBER
	CMPB	#NUMQ		IN RANGE?
	BHS	QERR		NO, INVALID
	LDX	#QFLAGS		PT TO FLAGS
	CLR	B,X		SET THE FLAG
	BRA	QUAL		GET NEXT
* QUALIFIER WAS INVALID, REPORT
QERR	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	24		SYSTEM REQUEST NUMBER
	FCCZ	/Invalid qualifier: '/
	LDA	,Y+		GET CHAR
QSHOW	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	33		SYSTEM REQUEST NUMBER
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	5		SYSTEM REQUEST NUMBER
	BEQ	QEND		EXIT
	CMPA	#'/'		ALSO TERMINATOR
	BNE	QSHOW		SHOW EM ALL
QEND	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	25		SYSTEM REQUEST NUMBER
	FCCZ	/'/		CLOSING QUITE
	LDA	#1		BAD OPERAND RC
ABORT	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	0		SYSTEM REQUEST NUMBER
* DISK TO DISK COPY PROGRAM
MAIN	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	16		SYSTEM REQUEST NUMBER
	BNE	ABORT		ERROR
	STA	>SOURCE		SAVE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	81		SYSTEM REQUEST NUMBER
	PSHS	A,B		SAVE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	16		SYSTEM REQUEST NUMBER
	BNE	ABORT		ERROR
	STA	>DEST		SAVE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	81		SYSTEM REQUEST NUMBER
	CMPD	,S++		MATCH?
	BEQ	MAIN1		YES
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	25		SYSTEM REQUEST NUMBER
	FCCZ	'Incompatable drive sizes'
	LDA	#100
	RTS
* COPY DISK
MAIN1	STD	>DSIZE		SAVE DRIVE SIZE
	LDA	>PROMPT
	BNE	CPY		DON'T PROMPT
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	88		SYSTEM REQUEST NUMBER
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	24		SYSTEM REQUEST NUMBER
	FCCZ	'Insert disks, press <return>:'
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	3		SYSTEM REQUEST NUMBER
CPY	LDA	>SOURCE		GET SOURCE DRIVE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	76		SYSTEM REQUEST NUMBER
	CMPA	>DEST		SAME DRIVE?
	BNE	CPY1		NO
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	24		SYSTEM REQUEST NUMBER
	FCB	$0D		CARRIAGE RETURN
	FCCZ	'Insert source disk, press <return>:'
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	3		SYSTEM REQUEST NUMBER
* READ A BUFFER INTO MEMORY
CPY1	LDA	>QUIET
	BEQ	QUI1
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	25		SYSTEM REQUEST NUMBER
	FCB	$0D		CARRIAGE RETURN
	FCCZ	'Reading'
QUI1	LDX	#BUFFER		PT TO RAM AREA
	LDY	#MAXBUF		ZERO COUNT
RDB1	LDD	>RSECT		GET SECTOR ID
	CMPD	>DSIZE		OVER?
	BHS	RDEND		YES, EXIT
	TST	>QUIET
	BEQ	QUI2		NO SECTOR COUNT
	LDA	#$0D		GET CR
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	33		SYSTEM REQUEST NUMBER
	LDD	>RSECT		RESTORE ADDRESS
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	26		SYSTEM REQUEST NUMBER
QUI2	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	92		SYSTEM REQUEST NUMBER
	LBNE	EXIT		ERROR
	LEAX	512,X		ADVANCE
	ADDD	#1		ADVANCE NUM
	STD	>RSECT		RESAVE
	LEAY	-1,Y		ADVANCE COUNT
	BNE	RDB1		NO, ITS OK
RDEND	LDA	>DEST		GET DEST
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	76		SYSTEM REQUEST NUMBER
	CMPA	>SOURCE		SAME AS SOURCE?
	BNE	CPY2		NO
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	24		SYSTEM REQUEST NUMBER
	FCB	$0D		CARRIAGE RETURN
	FCCZ	'Insert destination disk, press <return>:'
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	3		SYSTEM REQUEST NUMBER
* WRITE A BUFFER FROM MEMORY
CPY2	LDA	>QUIET
	BEQ	QUI3
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	25		SYSTEM REQUEST NUMBER
	FCB	$0D		CARRIAGE RETURN
	FCCZ	'Writing'
QUI3	LDX	#BUFFER		PT TO RAM AREA
	LDY	#MAXBUF		ZERO COUNT
WRB1	LDD	>WSECT		GET SECTOR ID
	CMPD	>DSIZE		OVER?
	BHS	WREND		IF SO, QUIT
	TST	>QUIET
	BEQ	QUI4
	LDA	#$0D		CARRIAGE RETURN
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	33		SYSTEM REQUEST NUMBER
	LDD	>WSECT		RESTORE SECTOR ID
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	26		SYSTEM REQUEST NUMBER
QUI4	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	93		SYSTEM REQUEST NUMBER
	BNE	EXIT		ERROR
	LEAX	512,X		NEXT
	ADDD	#1		ADVANCE
	STD	>WSECT		RESAVE
	LEAY	-1,Y		ADVANCE COUNT
	BNE	WRB1		MORE
WREND	LDD	>WSECT		RESTORE SECTOR
	CMPD	>DSIZE		OVER LIMIT
	LBLO	CPY		DO IT AGAIN SAM
	LDA	>QUIET
	BEQ	QUI5
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	22		SYSTEM REQUEST NUMBER
QUI5	CLRA
EXIT	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	0		SYSTEM REQUEST NUMBER
* QUALIFIER TEXT TABLE
QTABLE	FCB	$82		1 CHAR MATCH
	FCC	'/QUIET'
	FCB	$82
	FCC	'/PROMPT'
	FCB	$80		END OF TABLE
NUMQ	EQU	2		# OF QUALIFIERS
QFLAGS	EQU	*		QUALIFIER FLAG TABLE
QUIET	FCB	$FF		DON'T DISPLAY READ/WRITES
PROMPT	FCB	$FF		PROMPT FOR DISK SHANGE
* MISC VARAIBLES
RSECT	FDB	0		CURRENT READ SECTOR
WSECT	FDB	0		CURRENT WRITE SECTOR
SOURCE	RMB	1		SOURCE DRIVE
DEST	RMB	1		DESTINATION DRIVE
DSIZE	RMB	2		DRIVE SIZE
*
BUFFER	EQU	*
