*
* SYSTEM MEMORY LOCATIONS FOR USE BY APPLICATION PROGRAMS
*
OSRAM	EQU	$2000		USER APPLICATION RAM STARTS HEDE
OSEND	EQU	$DBFF		LAST LOCATION OF USER (NON-SYSTEM) RAM
OSUTIL	EQU	$D000		RAM FOR UTILITY PROGRAM USAGE
*
*
* CHGDISK: Change the disk between load and execution
*
* Copyright 1983-2005 Dave Dunfield
* All rights reserved.
*
* DIRECTORY STRUCTURE
	ORG	0
DPREFIX	RMB	8
DNAME	RMB	8
DTYPE	RMB	3
DDADR	RMB	2		DISK ADDRESS
DRADR	RMB	2		RUN ADDRESS
DATTR	RMB	1		FILE ATTRIBUTES
* ATTRIBUTE DEFINITIONS
RPERM	EQU	%10000000	READ PERMISSION
WPERM	EQU	%01000000	WRITE PERMISSSION
EPERM	EQU	%00100000	EXECUTE PERMISSION
DPERM	EQU	%00010000	DELETE PERMISSION
* RETURN CODES
RCPRO	EQU	3		PROTECTION VIOLATION
*
	ORG	OSUTIL-512
*
CHGDISK	CMPA	#'?'		HELP REQUEST?
	BNE	MAIN		NO, START IT UP
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	25		SYSTEM REQUEST NUMBER
	FCCZ	'Use: CHGDISK [<command string>]'
ABORT	RTS
MAIN	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	4		SYSTEM REQUEST NUMBER
	BNE	CMDSUP		YES
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	88		SYSTEM REQUEST NUMBER
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	25		SYSTEM REQUEST NUMBER
	FCCZ	'Insert command disk, and enter command:'
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	1		SYSTEM REQUEST NUMBER
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	4		SYSTEM REQUEST NUMBER
	BEQ	ABORT		NO, EXIT
* WE HAVE COMMAND TO EXECUTE
CMDSUP	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	12		SYSTEM REQUEST NUMBER
	LDD	#'EX'		FIRST TO OF 'EXE'
	STD	,X		SET TYPE
	STA	2,X		SET LAST 'E'
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	69		SYSTEM REQUEST NUMBER
	BNE	ABORT		ERROR
	LDA	DATTR,X		GET FILE ATTRIBUTES
	BITA	#EPERM		ALLOWED TO EXECUTE?
	BNE	EXEOK		OK TO EXECUTE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	45		SYSTEM REQUEST NUMBER
	RTS
* LOAD IN FILE & EXECUTE
EXEOK	LDD	DDADR,X		GET DISK ADDRESS
	LDX	DRADR,X		GET RUN ADDRESS
	STX	>EXEADR		SAVE ADDRESS
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	78		SYSTEM REQUEST NUMBER
* WE HAVE FILE IN RAM, PROMPT FOR DISK CHANGE, AND EXECUTE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	88		SYSTEM REQUEST NUMBER
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	24		SYSTEM REQUEST NUMBER
	FCCZ	'Insert disks for command execution, press <return>:'
CHKRET	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	34		SYSTEM REQUEST NUMBER
	CMPA	#$0D		CR?
	BNE	CHKRET		NO, WAIT FOR IT
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	22		SYSTEM REQUEST NUMBER
	JMP	[EXEADR]	EXECUTE COMMAND
* MISC LOCAL VARIABLES
EXEADR	RMB	2		EXECUTION ADDRESS OF COMMAND
