*
* SYSTEM MEMORY LOCATIONS FOR USE BY APPLICATION PROGRAMS
*
OSRAM	EQU	$2000		USER APPLICATION RAM STARTS HEDE
OSEND	EQU	$DBFF		LAST LOCATION OF USER (NON-SYSTEM) RAM
OSUTIL	EQU	$D000		RAM FOR UTILITY PROGRAM USAGE
*
*
* FLINK: Link/Unlink file directory entries
*
* Copyright 1983-2005 Dave Dunfield
* All rights reserved.
*
* DIRECTORY STRUCTURE
	ORG	0
FPREFIX	RMB	8		DIRECTORY PREFIX
FNAME	RMB	8		FILE NAME
FTYPE	RMB	3		FILE TYPE
FDADDR	RMB	2		DISK ADDRESS
FLADDR	RMB	2		LOAD ADDRESS
FPROT	RMB	1		FILE PROTECTIONS
FSPARE	RMB	8		USER SPARE
FDSIZE	EQU	*		SIZE OF DIR ENTRY
* PROTECTION BITS IN "FPROT" FIELD
RPERM	EQU	%10000000	READ PERMISSION
WPERM	EQU	%01000000	WRITE PERMISSION
EPERM	EQU	%00100000	EXECUTE PERMISSION
DPERM	EQU	%00010000	DELETE PERMISSION
	ORG	OSUTIL		UTILITY SPACE
*
FLINK	CMPA	#'?'		QUERY?
	BNE	QUAL		NO, TEST FOR QUALIFIERS
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	25		SYSTEM REQUEST NUMBER
	FCCZ	'Use: FLINK/UNLINK <file> [file ...]'
ABORT	RTS
* PARSE FOR COMMAND QUALIFIERS
QUAL	LDA	,Y		GET CHAR FROM COMMAND LINE
	CMPA	#'/'		IS IT A QUALIFIER?
	BNE	MAIN		NO, GET PARAMETERS
	LDX	#QTABLE		POINT TO QUALIFIER TABLE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	18		SYSTEM REQUEST NUMBER
	CMPB	#QMAX		IS IT IN RANGE
	BHS	QERR		IF SO, IT'S INVALID
	LDX	#QFLAGS		POINT TO QUALIFIER FLAGS
	CLR	B,X		SET THE FLAG
	BRA	QUAL		LOOK FOR ANOTHER QUALIFIER
* INVALID QUALIFIER RECEIVED
QERR	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	24		SYSTEM REQUEST NUMBER
	FCCZ	/Invalid qualifier: '/
	LDA	,Y+		GET CHARACTER
DSQU1	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	33		SYSTEM REQUEST NUMBER
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	5		SYSTEM REQUEST NUMBER
	BEQ	GOABO		END, EXIT
	CMPA	#'/'		NEXT QUALIFIER
	BNE	DSQU1		NO, KEEP DUMPING
GOABO	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	25		SYSTEM REQUEST NUMBER
	FCCZ	/'/		ENDING QUOTE
	LDA	#1		INVALID OPERAND RC
	RTS
*
* MAIN PROGRAM
*
MAIN	LDA	>QFLAGS		UNLINK MODE?
	BEQ	MAIN5		YES, DO UNLINK
*
* LINK MODE, LINK DIRECTORY ENTRIES TO EXISTING FILES
*
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	10		SYSTEM REQUEST NUMBER
	BNE	ABORT		ERROR, EXIT
	LDA	-1,X		GET DRIVE SPEC
	STA	>DRIVE		SAVE IT
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	69		SYSTEM REQUEST NUMBER
	BNE	ABORT		ERROR, EXIT
	LEAX	FDADDR,X	OFFSET TO DISK ADDRESS
	LDU	#SAVDIR		SAVE AREA
	LDB	#FDSIZE-FDADDR	GET SIZE
MAIN1	LDA	,X+		GET CHAR
	STA	,U+		SAVE IT
	DECB			REDUCE COUNT
	BNE	MAIN1		AND CONTINUE
* CREATE NEW FILE, AND COPY IN OTHER DIRECTORY INFO
MAIN2	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	10		SYSTEM REQUEST NUMBER
	BNE	ABORT		ERROR, EXIT
	LDA	-1,X		GET DRIVE SPEC
	CMPA	>DRIVE		IS IT SAME?
	BNE	MAIN4		NO, REPORT ERROR
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	72		SYSTEM REQUEST NUMBER
	BNE	ABORT		ERROR, EXIT
	STD	>SECTOR		SAVE SECTOR ID
	LEAX	FDADDR,X	OFFSET TO ADDRESS
	LDU	#SAVDIR		POINT TO SAVED ENTRY
	LDB	#FDSIZE-FDADDR	GET SIZE
MAIN3	LDA	,U+		GET CHAR
	STA	,X+		WRITE TO DIR
	DECB			REDUCE COUNT
	BNE	MAIN3		AND CONTINUE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	85		SYSTEM REQUEST NUMBER
	LDD	>SECTOR		GET SECTOR LINK OF NEW FILE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	80		SYSTEM REQUEST NUMBER
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	4		SYSTEM REQUEST NUMBER
	BNE	MAIN2		CONTINUE
	RTS
* LINK NAME SPECIFIED A DIFFERENT DRIVE THAN SOURCE
MAIN4	LDY	#ERRMSG		POINT TO ERROR MESSAGE
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	52		SYSTEM REQUEST NUMBER
	LDA	#100		ERROR CODE
	RTS
*
* UNLINK MODE, UNLINK FILES FROM DIRECTORY ENTRIES
*
MAIN5	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	10		SYSTEM REQUEST NUMBER
	BNE	ABORT1		ERROR, EXIT
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	69		SYSTEM REQUEST NUMBER
	BNE	ABORT1		ERROR, EXIT
	LDA	FPROT,X		GET FILE PROTECTIONS
	BITA	#DPERM		DELETE ALLOWED?
	BEQ	MAIN7		NO, REPORT ERROR
	LDB	#FDSIZE		GET DIRECTORY SIZE
MAIN6	CLR	,X+		ZERO ONE BYTE
	DECB			REDUCE COUNT
	BNE	MAIN6		DO THEM ALL
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	85		SYSTEM REQUEST NUMBER
	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	4		SYSTEM REQUEST NUMBER
	BNE	MAIN5		YES, HANDLE THEM
ABORT1	RTS
* FILE DOES NOT HAVE "DELETE" PERMISSION, REPORT VIOLATION
MAIN7	SWI			SOFTWARE INTERRUPT TO DOS
	FCB	45		SYSTEM REQUEST NUMBER
	RTS
* STRINGS & CONSTANTS
ERRMSG	FCCZ	'Cannot link between drives.'
QTABLE	FCB	$82
	FCC	'/UNLINK'
	FCB	$80		END OF TABLE
QMAX	EQU	1		# QUALIFIERS
* QUALIFIER FLAGS
QFLAGS	FCB	$FF		UNLINK MODE
* TEMPORARY STORAGE
DRIVE	RMB	1		SOURCE FILE DRIVE
SECTOR	RMB	2		NEW DISK SECTOR
SAVDIR	RMB	FDSIZE-FDADDR	SAVED DIR ENTRY
